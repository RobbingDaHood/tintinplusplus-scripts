#VARIABLE {auto_skin_enabled} {0};
#VARIABLE {battle_enabled} {0};  
#ALIAS {start_autoskin {.*}}            
{                                       
    #VARIABLE {auto_skin_enabled} {1};  
    #VARIABLE {battle_enabled} {1};  
    #VARIABLE thing_to_kill {%1};       
    #SEND {blackjack $thing_to_kill};        
    #SEND {backstab $thing_to_kill};        
    start_dustbomb_delay;
}                                       

#ACTION {{.*?} is hurt and suspicious}
{
    #SEND {kill $thing_to_kill};        
}

#ALIAS {stop_autoskin}
{
    #VARIABLE {auto_skin_enabled} {0};
    #VARIABLE {battle_enabled} {0};  
    #UNDELAY {dustbomb_delay}
}

#ACTION {{.*?} whom?}
{
    #VARIABLE {battle_enabled} {0};  
}

#ALIAS {start_dustbomb_delay}
{
    #IF {$auto_skin_enabled && $battle_enabled} 
    {
        #IF {"$current_health_percentage" != "100"} 
        {
            dustbomb;
        };
        #DELAY {dustbomb_delay} {start_dustbomb_delay} {3};
    };
}

#ACTION {You throw a dust bomb!}
{
    #IF {$auto_skin_enabled}
    {
        #DELAY {dustbomb_delay} {start_dustbomb_delay} {31};
    };
}

#ACTION {Your attack is absorbed}
{
    #IF {$auto_skin_enabled}
    {
        jab;
    };
}

#ACTION {(chilling touch) You}
{
    #IF {$auto_skin_enabled}
    {
        jab;
    };
}

#ACTION {(jab) You}
{
    #IF {$auto_skin_enabled}
    {
        kick;
    };
}

#ACTION {(kick) You}
{
    #IF {$auto_skin_enabled}
    {
        eyejab;
    };
}

#ACTION {is blinded by smoke!}
{

    #IF {$auto_skin_enabled}
    {
        #UNACTION {(kick) You};
    };
}

#ACTION {{.*?}slumps unconscious to the floor.}
{
    #IF {$auto_skin_enabled}
    {
        stop_autoskin;
    };
}

#ACTION {{.*?}R.I.P.}
{
    #IF {$auto_skin_enabled}
    {
        #VARIABLE {battle_enabled} {0};  
        get corpse;
    };
}

#ACTION {You get the corpse of {.*}}
{
    #IF {$auto_skin_enabled}
    {
        skin corpse;
    };
}

#ACTION {You finish pulling a bloody skin off the corpse of {.*}}
{
    craft inv put all;
    stop_autoskin;
}

#ALIAS {start_buff_self_delay}
{
    #IF {!$auto_skin_enabled && !$battle_enabled} 
    {
        buff self;
    };
    #DELAY {buff_self_delay} {start_buff_self_delay} {60};
}
