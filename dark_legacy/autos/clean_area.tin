# NOP { (large) The corpse of Hyakuhei lies here. };

#variable {state_autoexplore_enabled} {0};
#ALIAS {start_autoexplore}
{
    #variable {state_autoexplore_enabled} {1};
    recreate_map;
    draw sword;

    fprompt &Y%hhp &C%mm &G%vmv&w %o> [&R%D&w] FIGHTING_MOBS;
    prompt &Y%hhp &C%mm &G%vmv&w %o> FIGHTING_NO_MOBS;
    
    call pet;
    cast bear;

    #VARIABLE current_room_id -1;
    look;

    #DELAY {pause_autoexplore_delay} 
    {
        #SHOWME {Did automatic run for 28 minutes, so pausing.}; 
        pause_autoexplore;
    }{1680};
}

#VARIABLE fighting_mobs 0;
#ACTION {FIGHTING_MOBS}
{
    #VARIABLE fighting_mobs 1;
    delay_moving;
}

#ACTION {FIGHTING_NO_MOBS}
{
    #VARIABLE simple_spider_battle_enabled 0;
    #VARIABLE fighting_mobs 0;
    #map get all current_room_info;
    #SHOWME {$current_room_id != $current_room_info[vnum]};
    #IF {$current_room_id != $current_room_info[vnum]}
    {
        #VARIABLE current_room_id $current_room_info[vnum];
        #DELAY {look_for_exits_delay} {look} {1};
        #DELAY {move_to_closest_unexplored_room_delay} {move_to_closest_unexplored_room} {2};
        #DELAY {did_not_move_in_five_minutes_timer} 
        {
            #SHOWME {Did not move for five minutes, so pausing.}; 
            pause_autoexplore;
        }{300};
    };
}

#ACTION {is DEAD!!}
{
    #DELAY spidersilk_corpse_filler_delay spidersilk_corpse_filler {0.5};
    delay_moving;
}

#ACTION {A coin appears in your pouch as you sacrifice}
{
    #DELAY spidersilk_corpse_filler_delay spidersilk_corpse_filler {0.5};
    delay_moving;
}

#ALIAS {spidersilk_corpse_filler}
{
    #IF {!$fighting_mobs && $state_autoexplore_enabled}
    {
        fill spidersilkportal spider; 
        sacrifice spider;
    };
}

#ACTION {You have died}
{
    stop_autoexplore;
}

#ALIAS {stop_autoexplore}
{
    #variable {state_autoexplore_enabled} {0};
    fprompt &Y%hhp &C%mm &G%vmv&w %o> [&R%D&w];
    prompt &Y%hhp &C%mm &G%vmv&w %o>;
    #UNDELAY {pause_autoexplore_delay};
}

#ALIAS {pause_autoexplore}
{
    #variable {state_autoexplore_enabled} {0};
    fprompt &Y%hhp &C%mm &G%vmv&w %o> [&R%D&w] PAUSED;
    prompt &Y%hhp &C%mm &G%vmv&w %o> PAUSED;
    #UNDELAY {pause_autoexplore_delay};
}

#ALIAS {resume_autoexplore}
{
    #variable {state_autoexplore_enabled} {1};
    fprompt &Y%hhp &C%mm &G%vmv&w %o> [&R%D&w] FIGHTING_MOBS;
    prompt &Y%hhp &C%mm &G%vmv&w %o> FIGHTING_NO_MOBS;
    #DELAY {pause_autoexplore_delay} 
    {
        #SHOWME {Did automatic run for 28 minutes, so pausing.}; 
        pause_autoexplore;
    }{1680};
    #VARIABLE current_room_id -1;
    look;
}

#ACTION {A giant spider crawls along here.}
{
    #IF {$state_autoexplore_enabled}
    {
        delay_moving;
        simple_spider_battle spider;
    };
}

#ACTION {A small spider spins his web very carefully.}
{
    #IF {$state_autoexplore_enabled}
    {
        delay_moving;
        simple_spider_battle spider;
    };
}

#ACTION {A bundle of Spiders Thread lays here on the ground.}
{
    #IF {$state_autoexplore_enabled}
    {
        delay_moving;
        fill spidersilkportal 'spiders thread';
    };
}

#ACTION {The corpse of }
{
    delay_moving;
    spidersilk_corpse_filler;
}

#ACTION {Chiphulk the male young Chipmunk is DEAD!!}
{
    #NOP {When automating this then make it stop here};
}

#ACTION {A nest has been uncovered here.}
{
    delay_moving;
    fill randomloot nest;
    sacrifice nest;
}

#ALIAS {delay_moving}
{
    #UNDELAY {move_to_closest_unexplored_room_delay};
    #UNDELAY {look_for_exits_delay};
    #VARIABLE current_room_id -1;
}

#ACTION {With a fading growl, a bear}
{
    cast bear;
}

#ACTION {Ghost{.*?}Chiphulk}
{
    #NOP {cast resurrect pet;};
    order pet sit;
}

#ACTION {It's already full as it can be.}
{
    #IF {$state_autoexplore_enabled}
    {
        delay_moving;
        flush;
        label spidersilkportal fullspidersilkportal;
        label empty spidersilkportal;
        fill spidersilkportal 'spiders thread';
        fill spidersilkportal spider; 
    };
}
