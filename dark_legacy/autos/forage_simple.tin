#VARIABLE {max_with} {5};
#VARIABLE {max_height} {5};
#VARIABLE {current_with} {0};
#VARIABLE {current_height} {0};
#VARIABLE {square_auto_forage_enabled} {0};
#ALIAS {start_square_autoforage}
{
    #VARIABLE {square_auto_forage_enabled} {1};
    #VARIABLE {current_with} {0};
    #VARIABLE {current_height} {0};
    start_autoforage;
}

#ALIAS {stop_square_autoforage}
{
    #VARIABLE {square_auto_forage_enabled} {0};
    prompt &Y%hhp &C%mm &G%vmv&w %o>;
}

#ALIAS {choose_direction_auto_forage}
{
    #IF {$square_auto_forage_enabled}
    {
        flush;
        #IF {$max_height > $current_height}
        {
            #IF {$max_with == $current_with}
            {
                #VARIABLE {current_with} {0};
                #MATH {current_height} {$current_height + 1};
                n;
                w;
                w;
                w;
                w;
                w;
                #DELAY {start_autoforage_delay_trigger} {start_autoforage} {5};
            };
            #ELSEIF {$max_with > $current_with}
            {
                #MATH {current_with} {$current_with + 1};
                e;
                #DELAY {start_autoforage_delay_trigger} {start_autoforage} {5};
            };
            prompt &Y%hhp &C%mm &G%vmv&w %o> |$current_height, $current_with|;
        };
        #ELSE
        {
            stop_square_autoforage;
        };
    };
}

#ALIAS {start_autoforage}
{
    #VARIABLE {auto_forage_enabled} {1};
    fill garbage all;
    #DELAY {trigger_autoforage_look__delay} {look} {0.2};
    #DELAY {trigger_autoforage_delay} {trigger_autoforage} {1.0};
    sheathe; 
    draw pick;
}

#ALIAS {start_autofish}
{
    #VARIABLE {auto_fish_enabled} {1};
    trigger_autofish;
}

#VARIABLE {auto_forage_enabled} {0};
#ALIAS {stop_autoforage}
{
    #VARIABLE {auto_forage_enabled} {0};
    fill_forageportal;
    look;
    #DELAY {choose_direction_auto_forage_delay} {choose_direction_auto_forage} {0.5};
}

#ALIAS {fill_forageportal}
{
    put all.pine forageportal;
    put all.cotton rawcotton;
    put all.log forageportal;
    put all.thistle forageportal;
    put all.eye forageportal;
    put all.bloodroot forageportal;
    put all.star forageportal;
    put all.medusa forageportal;
}

#VARIABLE {auto_fish_enabled} {0};
#ALIAS {stop_autofish}
{
    #VARIABLE {auto_fish_enabled} {0};
}

#ACTION {You are done harvesting}
{
    trigger_autoforage;
}

#ACTION {You finish a}
{
    trigger_autoforage;
}

#ACTION {You salvaged all the good bits}
{
    trigger_autoforage;
}

#ACTION {You plucked a cotton shrub bare.}
{
    trigger_autoforage;
}

#ACTION {You feel too tired to continue}
{
    trigger_refresh;
    trigger_autoforage;
    trigger_autofish;
}

#ACTION {You are too exhausted, need at least}
{
    trigger_refresh;
    trigger_autoforage;
    trigger_autofish;
}

#ACTION {Nothing to harvest from that.}
{
    stop_autoforage;
}

#ACTION {After catching nothing for quite a while you reel in your fishing line.}
{
    stop_autofish;
}

#ALIAS {trigger_autoforage}
{
    #IF {$auto_forage_enabled}
    {
        forage;
    };
};

#ALIAS {trigger_autofish}
{
    #IF {$auto_fish_enabled}
    {
        fish water;
    };
};

#ALIAS {trigger_refresh}
{
    sheathe;
    cast refresh;
    cast refresh;
    cast refresh;
    cast refresh;
};

#ACTION {No such object.}
{
    stop_square_autoforage;
}

#ACTION {does not fit into an extradimensional portal}
{
    #VARIABLE square_auto_forage_enabled 0;
    flush;
    #DELAY {move_portal_and_create_a_new_one_delay} {move_portal_and_create_a_new_one} {0.5};
}

#ALIAS {move_portal_and_create_a_new_one}
{
    label forageportal forageportalfulltest;
    label backupforageportal forageportal;
    fill_forageportal;
    cast Extradimensional;
    label -1.portal backupforageportal;
    #VARIABLE square_auto_forage_enabled 1;
    choose_direction_auto_forage;
}
