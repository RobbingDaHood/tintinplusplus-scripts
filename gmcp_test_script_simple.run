#READ {pr_login.tin};
#split;

#ALIAS {start_gmcp}
{
    config gmcp on; 
    config gmcpseenrooms on;
}

#ALIAS {store_if_exist} 
{
    #nop {the variable is not empty and is not the same as currently stored value};
    #if {&%1 != 0 && "$%1" != "$%2"}
    {
        #variable {%2} {$%1};
        #IF {"%2" != "affects" && "%2" != "skills"}
        {
            #nop {#showme {changing %2 from $%2 to $%1};};
        };
        #IF {"%2" == "room_entities"}
        {
            look_at_all_entities;
            #variable {did_any_data_change} {1};
        };
        #IF {"%2" == "room_items"}
        {
            #variable {room_item_number} {&room_items[]};
            #variable {did_any_data_change} {1};
        };
        #IF {"%2" == "room_id"}
        {
            #variable {did_any_data_change} {1};
        };
    } 
}

#EVENT {IAC SB GMCP}
{
    #nop {Just contains the next GMCP event name, so not interesting.};
    
    #if {"%0" != "Char.Vitals" && "%0" != "Char.State" && "%0" != "room.info" && "%0" != "Char.Output" && "%0" != "Client.Media.Stop" && "%0" != "Client.Media.Play"}
    {
        #showme {GMCP: %0};
    }
}

#EVENT {IAC SB GMCP Char.Vitals IAC SE}
{
    #nop {Example values: gmcp_vitals: {combo}{0}{en}{162}{food}{78}{hp}{437}{level}{30}{maxcombo}{100}{maxen}{187}{maxfood}{110}{maxhp}{437}{maxrage}{100}{maxst}{208}{nl}{531050}{rage}{0}{st}{201}{xp}{492243}}
}

#EVENT {IAC SB GMCP room.info IAC SE}
{
    #nop {Example values for each room in range: gmcp_room_info: {exits}{{e}{1938569184}{n}{582155329}{ne}{254486512}{nw}{46100561}{s}{1161239236}{se}{7612 82581}{sw}{1121466420}{w}{1730183233}}{generated}{true}{name}{Gold Mine Tailings}{num}{1194128465}{terrain }{Gold Mine Tailings}{x}{265}{y}{359}{zone}{The Great Plains}{zoneId}{plains}}
}

#EVENT {IAC SB GMCP Char.Output IAC SE}
{
    #variable {latest_gmcp_output} {%0};

    #regex {$latest_gmcp_output[id]} {look_at_entity}
    {
        #cat {entities_looked_at_in_the_room[$latest_gmcp_output[id]]} {$latest_gmcp_output[output]};
        #regexp {$latest_gmcp_output[output]} {Robbingdahood}
        {}
        {
            #regexp {$latest_gmcp_output[output]} {You are using}
            {}
            {
                #regex {$latest_gmcp_output[output]} {31m{[^\*]*?}u001b[0m} 
                {
                    #variable {enemy_present_in_room} {1}
                };
            }
        }
    };

    updated_all_data;
}

#variable {enemy_present_in_room} {0};
#variable {entities_looked_at_in_the_room} {};
#variable {entities_looking_at_in_the_room} {0};
#ALIAS {look_at_all_entities}
{
    #variable {enemy_present_in_room} {0};
    #variable {entities_looked_at_in_the_room} {};
    #variable {entities_looking_at_in_the_room} {&room_info[entities][]};
    #foreach {$room_info[entities][]} {entity} 
    {
        #send {gmcp cmd look_at_entity$entity look eid:$entity}; 
    };
}

#ALIAS {reload_configs}
{
    #kill; 
    #read gmcp_test_script_simple.run;
}

#EVENT {IAC SB GMCP Char.State IAC SE}
{
    #variable {updated_charstate}{0};
    #variable {did_any_data_change} {0};
    #variable gmcp_char_state {%0};
    
    store_if_exist gmcp_char_state[update][room] room_info;
    store_if_exist gmcp_char_state[update][room][id] room_id;
    store_if_exist gmcp_char_state[update][room][x] room_x;
    store_if_exist gmcp_char_state[update][room][y] room_y;
    store_if_exist gmcp_char_state[update][room][exits] room_exits;
    store_if_exist gmcp_char_state[update][room][items] room_items;
    store_if_exist gmcp_char_state[update][skills] skills;
    store_if_exist gmcp_char_state[update][affects] affects;
    store_if_exist gmcp_char_state[update][room][entities] room_entities;
    #variable {battle_info} {$gmcp_char_state[update][battle]};

    #variable {updated_charstate}{1};
    updated_all_data;
}

#ALIAS {start_autoexplore}
{
    #map destroy world;
    #map create 999999;
    #map goto 1;
    #variable {state_autoexplore} {1};
    #variable {autoexplore_start_room_id} {$room_id};
    register_current_room_in_map;
}

#ALIAS {register_current_room_in_map} 
{
    #map vnum $room_id;
}

 #VARIABLE {short_to_long_exit_name} 
{
    {e}{east}
    {ne}{northeast}
    {n}{north}
    {nw}{northwest}
    {w}{west}
    {sw}{southwest}
    {s}{south}
    {se}{southeast}
}

#VARIABLE {long_to_short_exit_name} 
{
    {east}{e}
    {northeast}{ne}
    {north}{n}
    {northwest}{nw}
    {west}{w}
    {southwest}{sw}
    {south}{s}
    {southeast}{se}
}

#ALIAS {map_all_exits}
{
    #foreach {$room_exits[%*]} {room_exit}
    {
        #map dig {$long_to_short_exit_name[$room_exit]}; 
    };
    #map set {roomdesc} {explored};
}

#EVENT {MAP CREATE ROOM}
{
    #if {$state_autoexplore}
    {
        #map set {roomdesc} {unexplored} {%0};
    }
}

#ALIAS {walk_towards_unexplored}
{
    #map find {roomdesc} {unexplored};
    #path get length length_of_path;
    #if {$length_of_path > 0} 
    {
        #path walk;
    };
    #else 
    {
        #showme {There are no more unexplored rooms: Congrats!};
    };
}

#ALIAS {next_step}
{
    #if {!$performing_action}
    {
        map_all_exits;
        walk_towards_unexplored;
    };
}

#ALIAS {start_autokill}
{
    #var {state_autokill} {1};
    config autobattle on;
}

#ALIAS {stop_autokill}
{
    #var {state_autokill} {0};
}

#variable {is_updated_all_data} {0};
#ALIAS {updated_all_data}
{
    #IF {$entities_looking_at_in_the_room == &entities_looked_at_in_the_room[] && $updated_charstate && $did_any_data_change}
    {
        #variable {is_updated_all_data} {1};
        #delay {doing_next_delay} {do_next_action} {1};
    };
    #else
    {
        #variable {is_updated_all_data} {0};
    };
}

#ALIAS {do_next_action} 
{
    #if {&battle_info[] == 0}
    {
            #IF {$state_autokill && $enemy_present_in_room} 
            {
                kill;
            };
            #elseif {$room_item_number < 7 && !$enemy_present_in_room} 
            {
                #showme {$room_item_number};
            };
    };
    #else 
    {
        #showme {I am in battle!};
    };
}

start_gmcp;
